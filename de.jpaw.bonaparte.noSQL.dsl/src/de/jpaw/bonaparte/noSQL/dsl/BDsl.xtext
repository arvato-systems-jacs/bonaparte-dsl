grammar de.jpaw.bonaparte.noSQL.dsl.BDsl with de.jpaw.bonaparte.dsl.BonScript

generate bDsl "http://www.jpaw.de/bonaparte/noSQL/dsl/BDsl"
 /*
  * Copyright 2014 Michael Bischoff
  *
  * Licensed under the Apache License, Version 2.0 (the "License");
  * you may not use this file except in compliance with the License.
  * You may obtain a copy of the License at
  *
  *   http://www.apache.org/licenses/LICENSE-2.0
  *
  * Unless required by applicable law or agreed to in writing, software
  * distributed under the License is distributed on an "AS IS" BASIS,
  * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  * See the License for the specific language governing permissions and
  * limitations under the License.
  */
  
import "http://www.jpaw.de/bonaparte/dsl/BonScript" as pojos
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes    // to allow checking of referenced imports

Model:
    includes        += IncludeDefinition*
    imports         += ImportDefinition*
    (defaults        = DefaultsDefinition)?
    packages        += PackageDefinition*
;
    
QualifiedId: 
    ID ('.' ID)*
;

QualifiedIdWithWildcard: 
    QualifiedId ('.*')?
;

IncludeDefinition:
    'include' importURI=STRING
;
ImportDefinition:
    'import' importedNamespace=QualifiedIdWithWildcard
;

DefaultsDefinition:
    'default' {DefaultsDefinition}
    ('setnamepattern' namePattern=STRING ('dropSuffix' dropSuffix=STRING)?)?
    ('namespace' tablespaceName=ID)?
    ';'
;

TableCategoryDefinition:
    'category' name=ID '{'
    ('trackinggroup' trackingColumns=[pojos::ClassDefinition])?
    ('setnamepattern' namePattern=STRING ('dropSuffix' dropSuffix=STRING)?)?
    ('namespace' tablespaceName=ID)?
    '}'
;

PackageDefinition:
    (javadoc = JAVADOC_COMMENT)?
    'package' ('(' prefix=QualifiedId ')')? name=QualifiedId // prefix is generated but not stored in serialized form, default is de.jpaw.bonaparte.pojos
        ('bundle' bundle = QualifiedId)?  // bundles are coarser structures than packages, they correspond to OSGi bundles
        'prefix' dbPrefix = ID
    '{' (
         tableCategories += TableCategoryDefinition
        | entities += EntityDefinition
        )*
    '}'
;


EntityDefinition:
    (javadoc = JAVADOC_COMMENT)?
    (isDeprecated?='@Deprecated')?
    (isAbstract?='abstract')?
    'entity' name = ID      // abstract means mappedSuperclass
        // 'type' tabletype=TableType
        'category' tableCategory=[TableCategoryDefinition]
        'is' pojoType=[pojos::ClassDefinition]
        ('setname' tablename = ID)?  // overwrite auto-generated tablename
        ('namespace' tablespaceName=ID)?
        (('inherits' (extends=[EntityDefinition|QualifiedId])))?
        (('extends' extendsClass=[pojos::ClassDefinition])  // unused / unusable? or mapped superclass?
         | ('extendsJava' extendsJava=QualifiedId)
        )?
        ('implements' implementsJavaInterface = [jvmTypes::JvmGenericType|QualifiedId] )?
    '{'
        ('tenantClass' tenantClass = [pojos::ClassDefinition])?
        ('tenant' tenantId = SingleColumn)?
        ('pk'     ('is' pkPojo = [pojos::ClassDefinition])? (pk       = ListOfColumns)? )?
        ('index'  index   += IndexDefinition)*
    '}'       
;

IndexDefinition:
    columns = SingleColumn
;

// by custom ScopeProvider implementation, an appropriate importedNamespace will be added here
ListOfColumns:
    '(' columnName += [pojos::FieldDefinition] (',' columnName += [pojos::FieldDefinition])* ')'    
;
// by custom ScopeProvider implementation, an appropriate importedNamespace will be added here
SingleColumn:
    singleColumnName = [pojos::FieldDefinition]    
;
